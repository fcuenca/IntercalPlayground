	PLEASE DON'T LAUGH. 
	
	PLEASE NOTE INCLUSION OF EXTENDED STANDARD LIBRARY
	PLEASE DO (1984) NEXT
	
	PLEASE NOTE: ALWAYS RUN THE UNIT TESTS WHEN REFACTORING
	DO %99 ABSTAIN FROM (1)
(1)	DO (99) NEXT
	
	PLEASE NOTE: THIS IS THE INTERACTIVE FIZZ-BUZZ PROGRAM. ENTER ZERO TO END.
	
(4)	DO (5) NEXT
(5)	DO FORGET #1
	DO WRITE IN .100
	DO .1 <- '?"'.100~.100'~#1"$#1'~#3
	DO (10) NEXT

	PLEASE NOTE CALCULATE THE FIZZBUZZ OF INPUT
	DO (100) NEXT
	DO READ OUT .101
	DO (4) NEXT

(10)	DO (11) NEXT
	PLEASE NOTE USER ENTERED ZERO AND WANTS TO QUIT
	PLEASE GIVE UP

(11)	DO RESUME .1

(99)	PLEASE NOTE THIS ARE THE UNIT TESTS FOR FIZZ-BUZZ
	DO FORGET #1
	DO (5000) NEXT

	PLEASE NOTE ASSERT-EQUAL(#1, FIZZ-BUZZ OF #1)
	DO .100 <- #1
	DO (100) NEXT
	DO ,500 SUB #1 <- #1
	DO ,500 SUB #2 <- .101
	DO (5001) NEXT

	PLEASE NOTE ASSERT-EQUAL(#2, FIZZ-BUZZ OF #2)
	DO .100 <- #2
	DO (100) NEXT
	DO ,500 SUB #1 <- #2
	DO ,500 SUB #2 <- .101
	DO (5001) NEXT

	PLEASE NOTE ASSERT-EQUAL(0xF000, FIZZ-BUZZ OF #3)
	DO .100 <- #3
	DO (100) NEXT
	DO ,500 SUB #1 <- #61440
	DO ,500 SUB #2 <- .101
	DO (5001) NEXT

	PLEASE NOTE ASSERT-EQUAL(0xB000, FIZZ-BUZZ OF #5)
	DO .100 <- #5
	DO (100) NEXT
	DO ,500 SUB #1 <- #45056
	DO ,500 SUB #2 <- .101
	DO (5001) NEXT

	PLEASE NOTE ASSERT-EQUAL(0xFB00, FIZZ-BUZZ OF #15)
	DO .100 <- #15
	DO (100) NEXT
	DO ,500 SUB #1 <- #64256
	DO ,500 SUB #2 <- .101
	DO (5001) NEXT

	DO GIVE UP

(100)	PLEASE NOTE THIS IS THE FIZZBUZZ FUNCTION	

	PLEASE NOTE: IS THE INPUT DIVISIBLE BY #15?
	DO .1 <- .100	
	DO .2 <- #15
	DO (2030) NEXT
	PLEASE NOTE: is .4 (remainder) == 0?
	DO .4 <- '?"'.4~.4'~#1"$#1'~#3
	DO (130) NEXT
	
	PLEASE NOTE NUMBER IS NOT DIVISIBLE BY #15 => CHECK IF DIVISIBLE BY #3
	DO .2 <- #3
	DO (2030) NEXT
	PLEASE NOTE: is .4 (remainder) == 0?
	DO .4 <- '?"'.4~.4'~#1"$#1'~#3
	DO (110) NEXT

	PLEASE NOTE NUMBER IS NOT DIVISIBLE BY #3 => CHECK IF DIVISIBLE BY #5
	DO .2 <- #5
	DO (2030) NEXT
	PLEASE NOTE: is .4 (remainder) == 0?
	DO .4 <- '?"'.4~.4'~#1"$#1'~#3
	DO (120) NEXT
	
	PLEASE NOTE NUMBER IS REGULAR => RETURN THE INPUT
	DO .101 <- .100
	DO (199) NEXT

(110)	DO (111) NEXT
	DO FORGET #1
	PLEASE NOTE NUMBER IS DIVISIBLE BY #3 => RETURN FIZZ
	DO .101 <- #61440
	DO (199) NEXT


(120)	DO (111) NEXT
	DO FORGET #1
	PLEASE NOTE NUMBER IS DIVISIBLE BY #5 => RETURN BUZZ
	DO .101 <- #45056
	DO (199) NEXT

(130)	DO (111) NEXT
	DO FORGET #1
	PLEASE NOTE NUMBER IS DIVISIBLE BY #15 => RETURN FIZZ-BUZZ
	DO .101 <- #64256
	DO (199) NEXT

(111)	DO RESUME .4
	
(199)	DO FORGET #1
	DO RESUME #1

